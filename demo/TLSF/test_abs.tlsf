INFO{
	TITLE: "Controller synthesis for continuousdynamical systems"
	DESCRIPTION:"none"
	SEMANTICS:   Mealy,Strict
	TARGET:   Mealy
}
GLOBAL  {
	PARAMETERS {
		n = 4; //num of states
		m = 9; //num of actions
	}
	DEFINITIONS {
	mutual(b) =
	||[i IN {0, 1 .. (SIZEOF b) -1}]
	&&[j IN {0,1 .. (SIZEOF b) -1} (\) {i}]
	(b[i] && !b[j]);
	}
}
MAIN{
	INPUTS {
	STATE[n];
	}
	OUTPUTS {
	ACTION[m];
	}
	INITIALLY{
	mutual(STATE);
	}
	PRESET {
	}
	REQUIRE {
	mutual(STATE);
	(STATE[0] && ACTION[0]) -> ( X STATE[0] );
	(STATE[0] && ACTION[1]) -> ( X STATE[1] );
	(STATE[1] && ACTION[2]) -> ( X STATE[0] );
	(STATE[1] && ACTION[3]) -> ( X STATE[3] );
	(STATE[3] && ACTION[4]) -> ( X STATE[1] ||  X STATE[2] );
	(STATE[1] && ACTION[5]) -> ( X STATE[2] );
	(STATE[2] && ACTION[6]) -> ( X STATE[1] ||  X STATE[3] );
	(STATE[2] && ACTION[7]) -> ( X STATE[2] );
	(STATE[3] && ACTION[8]) -> ( X STATE[3] );
	}
	ASSUME {
	G F ( !(STATE[2] || STATE[3]) || !(ACTION[4] || ACTION[6]));
	}
	ASSERT {
	mutual(ACTION);
	STATE[0] -> (ACTION[0] || ACTION[1]);
	STATE[1] -> (ACTION[2] || ACTION[3] || ACTION[5]);
	STATE[2] -> (ACTION[6] || ACTION[7]);
	STATE[3] -> (ACTION[4] || ACTION[8]);
	}
	GUARANTEE {
	G F ( STATE[0] );
	G F ( STATE[2] || STATE[3] );
	}
}